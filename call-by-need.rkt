#lang racket
(require "match.rkt" "env0.rkt" "truep.rkt")
;<exp> ::= <int>
;       |  <bool>
;       |  <var>
;       |  (if <exp> <exp> <exp>)
;       |  (lambda <var> <exp>)
;       |  (let <var> <exp> <exp>)
;       |  (random <exp>)
;       |  (<op> <exp> <exp>)
;       |  (<exp> <exp>)
;<op> ::= + | - | * | =
(define (interp exp env)
  (match exp
    (,int (guard (integer? int)) int)
    (,bool (guard (boolean? bool)) bool)
    (,var (guard (symbol? var)) (force-thunk (apply-env env var)))
    ((if ,e1 ,e2 ,e3)
     (let ((v1 (interp e1 env)))
       (if (true? v1)
           (interp e2 env)
           (interp e3 env))))
    ((lambda ,x ,body)
     (make-closure x body env))
    ((let ,x ,e ,body)
     (let* ((t (build-thunk e env))
            (env^ (extend-env x t env)))
       (interp body env^)))
    ((random ,e)
     (let ((v (interp e env)))
       (random v)))
    ((,op ,e1 ,e2)
     (guard (memq op '(+ - * =)))
     (let* ((v1 (interp e1 env))
            (v2 (interp e2 env)))
       (case op
         ((+) (+ v1 v2))
         ((-) (- v1 v2))
         ((*) (* v1 v2))
         ((=) (= v1 v2)))))
    ((,rator ,rand)
     (let* ((closure (interp rator env))
            (arg (build-thunk rand env)))
       (apply-closure closure arg)))))
(define (make-closure formal body env)
  (vector 'closure formal body env))
(define (closure-formal closure)
  (vector-ref closure 1))
(define (closure-body closure)
  (vector-ref closure 2))
(define (closure-env closure)
  (vector-ref closure 3))
(define (apply-closure closure arg)
  (let* ((formal (closure-formal closure))
         (body (closure-body closure))
         (env (closure-env closure)))
    (interp body (extend-env formal arg env))))
(define (make-thunk exp env flag val)
  (vector 'thunk exp env flag val))
(define (build-thunk exp env)
  (make-thunk exp env #f 'uninitialized))
(define (thunk-exp thunk)
  (vector-ref thunk 1))
(define (thunk-env thunk)
  (vector-ref thunk 2))
(define (thunk-flag thunk)
  (vector-ref thunk 3))
(define (thunk-val thunk)
  (vector-ref thunk 4))
(define (set-thunk-flag! thunk flag)
  (vector-set! thunk 3 flag))
(define (set-thunk-val! thunk val)
  (vector-set! thunk 4 val))
(define (force-thunk thunk)
  (if (thunk-flag thunk)
      (thunk-val thunk)
      (let* ((exp (thunk-exp thunk))
             (env (thunk-env thunk))
             (val (interp exp env)))
        (set-thunk-flag! thunk #t)
        (set-thunk-val! thunk val)
        val)))
;> (interp '((lambda n
;              (if (= n 0)
;                  (if (= n 0)
;                      (if (= n 0)
;                          (if (= n 0)
;                              (if (= n 0)
;                                  (if (= n 0)
;                                      (if (= n 0) #t #f)
;                                      #f)
;                                  #f)
;                              #f)
;                          #f)
;                      #f)
;                  (if (= n 0)
;                      #f
;                      (if (= n 0)
;                          #f
;                          (if (= n 0)
;                              #f
;                              (if (= n 0)
;                                  #f
;                                  (if (= n 0)
;                                      #f
;                                      (if (= n 0)
;                                          #f
;                                          #t))))))))
;            (random 2))
;          (empty-env))
;#t